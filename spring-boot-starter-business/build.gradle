plugins {
	id 'org.springframework.boot' version '2.1.4.RELEASE'
	id 'java'
  id 'com.github.sherter.google-java-format' version '0.8'
}

apply plugin: 'io.spring.dependency-management'
apply plugin: 'idea'
apply plugin: 'eclipse'

description = 'Spring Boot Starter'

group = 'com.example'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8
targetCompatibility = 1.8

googleJavaFormat {
  options style: 'AOSP'
  source = sourceSets*.allJava
  include '**/*.java'
  exclude '**/temp/*.java'
  exclude '**/solidity/*.java'
}

verifyGoogleJavaFormat {
  source = sourceSets*.allJava
  include '**/*.java'
  exclude '**/temp/*.java'
  exclude '**/solidity/*.java'
}

check.dependsOn.remove(test)
check.dependsOn.remove(verifyGoogleJavaFormat)

repositories {
    maven { url "http://maven.aliyun.com/nexus/content/groups/public/" }
    maven { url "https://dl.bintray.com/ethereum/maven/" }
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    mavenCentral()
}
configurations {
    all {
        exclude group: 'org.slf4j', module: 'slf4j-log4j12'
    }
    compileOnly {
        extendsFrom annotationProcessor
    }
    mybatisGenerator
}
dependencies {
	compile 'org.springframework.boot:spring-boot-starter-logging'
	compile 'org.codehaus.groovy:groovy-all:2.5.6'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    compile 'org.springframework.boot:spring-boot-starter-web'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
    compile "org.mybatis:mybatis:3.4.1"
    compile "org.mybatis:mybatis-spring:1.3.1"
    compile("org.mybatis.spring.boot:mybatis-spring-boot-starter:1.3.0")
    compile 'mysql:mysql-connector-java:8.0.11'
    compile 'com.qiniu:qiniu-java-sdk:7.2.18'
    compile group: 'io.netty', name: 'netty-all', version: '4.1.15.Final'
    testCompile group: 'junit', name: 'junit', version: '4.12'
    //2、添加依赖
    mybatisGenerator 'org.mybatis.generator:mybatis-generator-core:1.3.5'
    mybatisGenerator 'mysql:mysql-connector-java:8.0.11'
    mybatisGenerator 'tk.mybatis:mapper:3.3.9'
    
  compile 'org.fisco-bcos:web3sdk:2.3.0'
}



test {
    systemProperty "jdk.tls.namedGroups", "secp256k1"
}
//3、添加任务
def getDbProperties = {
    def properties = new Properties()
    file("src/main/resources/mybatis/db-mysql.properties").withInputStream { inputStream ->
        properties.load(inputStream)
    }
    properties;
}
task mybatisGenerate doLast {
    def properties = getDbProperties()
    ant.properties['targetProject'] = projectDir.path
    ant.properties['driverClass'] = properties.getProperty("jdbc.driverClassName")
    ant.properties['connectionURL'] = properties.getProperty("jdbc.url")
    ant.properties['userId'] = properties.getProperty("jdbc.user")
    ant.properties['password'] = properties.getProperty("jdbc.pass")
    ant.properties['src_main_java'] = sourceSets.main.java.srcDirs[0].path
    ant.properties['src_main_resources'] = sourceSets.main.resources.srcDirs[0].path
    ant.properties['modelPackage'] = properties.getProperty("package.model")
    ant.properties['mapperPackage'] = properties.getProperty("package.mapper")
    ant.properties['sqlMapperPackage'] =properties.getProperty("package.xml")
    ant.taskdef(
            name: 'mbgenerator',
            classname: 'org.mybatis.generator.ant.GeneratorAntTask',
            classpath: configurations.mybatisGenerator.asPath
    )
    ant.mbgenerator(overwrite: true,
            configfile: 'src/main/resources/mybatis/generatorConfig.xml', verbose: true) {
        propertyset {
            propertyref(name: 'targetProject')
            propertyref(name: 'userId')
            propertyref(name: 'driverClass')
            propertyref(name: 'connectionURL')
            propertyref(name: 'password')
            propertyref(name: 'src_main_java')
            propertyref(name: 'src_main_resources')
            propertyref(name: 'modelPackage')
            propertyref(name: 'mapperPackage')
            propertyref(name: 'sqlMapperPackage')
        }
    }
}
